#define LightingName PBR

// TODO: Define in the including code
#define PI 3.14158265359

struct PBRVertex {
	vec3 normal_worldSpace;
	vec3 viewDirection_worldSpace;
};
PBRVertex PBRFillVertex(vec3 viewDirection, vec3 normal, vec3 tangent){
	PBRVertex vertex;
	vertex.normal_worldSpace = normal;
	vertex.viewDirection_worldSpace = viewDirection;
	return vertex;
}

struct PBRFragment {
	vec4 albedo;
	float roughness;
	float metallic;
	
	vec3 normal_worldSpace;
};
TURNIP_FRAGMENT_TEXTURE(0, albedoTexture);
TURNIP_UNIFORM(1) vec4 color;
PBRFragment PBRFillFragment(PBRVertex vertex, vec2 uv0){
	PBRFragment fragment;
	fragment.albedo = pow(TURNIP_FRAGMENT_SAMPLE(albedoTexture, uv0), vec4(2.2, 2.2, 2.2, 1));
	fragment.roughness = 0.1f;
	fragment.metallic = 0.0f;

	fragment.normal_worldSpace = vertex.normal_worldSpace;
	return fragment;
}

float PBRLightingDot(vec3 one, vec3 two){
	return max(0.0, dot(one, two));
}
float PBRTrowbridgeReitzGGX(float NdotH, float roughness){
	float roughness2 = roughness * roughness * roughness * roughness; // Disney thingy: Use roughness^4 instead of roughness^2 to look more natural
	float NdotH2 = NdotH * NdotH;

	float nom = roughness2;
	float denom = (NdotH2 * (roughness2 - 1.0) + 1.0);
	denom = PI * denom * denom;

	return nom/max(denom, 0.0001);
}
float PBRSchlickGGX(float NdotVec, float k){
	float nom = NdotVec;
	float denom = NdotVec * (1 - k) + k;

	return nom/max(denom, 0.0001);
}
float PBRSmithTotalGeometryGGX(float NdotV, float NdotL, float k){
	return PBRSchlickGGX(NdotV, k) * PBRSchlickGGX(NdotL, k);
}
float PBRKFromDirect(float roughness){
	return (roughness + 1) * (roughness + 1) / 8.0;
}
float PBRKFromIBL(float roughness){
	return (roughness * roughness) / 2.0;
}
vec3 PBRSchlickFresnel(float HdotV, vec3 fresnelColor){
	//return HdotV + fresnelColor * pow(1.0 - HdotV, 5);
	return mix(fresnelColor, vec3(1), pow(1.0 - HdotV, 5));
}
// TODO: Roughness should contribute to this for IBL (see learnopengl.com)
vec3 PBRFresnelColor(vec3 albedo, float metallic){
	return mix(vec3(0.04), albedo, metallic);
}
vec3 PBRDirectContribution(vec3 brdfF, float NdotH, float NdotV, float NdotL, PBRVertex vertex, PBRFragment fragment, TurnipLight light){
	float brdfNDF = PBRTrowbridgeReitzGGX(NdotH, fragment.roughness);
	float brdfG = PBRSmithTotalGeometryGGX(NdotV, NdotL,
										   PBRKFromDirect(fragment.roughness));
	vec3 specularFraction = brdfF;
	//return vec4(specularFraction, 0);
	vec3 specularNumerator = vec3(brdfNDF * brdfG); // Miss out F because it is the specularFraction
	//return vec4(specularNumerator, 0);
	vec3 specularDenominator = vec3(4 * NdotV * NdotL);
	vec3 specularColor = specularNumerator / max(specularDenominator, 0.00001);
	//return vec4(brdfNDF, brdfG, brdfF.x, 1);
	//return vec4(specularColor, 0);
	//return vec4(specularColor * specularFraction, 0);
	
	vec3 diffuseFraction = (1 - specularFraction) * (1 - fragment.metallic);
	vec3 diffuseColor = fragment.albedo.rgb;// / PI;

	return (diffuseColor * diffuseFraction + specularFraction * specularColor) * light.radiance * NdotL;
}
vec3 PBRIndirectContribution(vec3 brdfF, PBRVertex vertex, PBRFragment fragment){
	vec3 specularFraction = brdfF;
	vec3 diffuseFraction = 1.0 - specularFraction;

	vec3 irradiance = vec3(0.5, 1.0, 1.0) * 0.5f;

	vec3 diffuseColor = irradiance * fragment.albedo.rgb;
	vec3 specularColor = vec3(0); // TODO
	
	return diffuseColor * diffuseFraction + specularColor * specularFraction;
}
vec4 PBRLighting(PBRVertex vertex, PBRFragment fragment, TurnipLight light){
	vec3 L = normalize(light.direction_worldSpace);
	vec3 V = normalize(vertex.viewDirection_worldSpace);
	vec3 N = normalize(fragment.normal_worldSpace);
	vec3 H = normalize(V+L);

	float NdotH = PBRLightingDot(N, H);
	float NdotV = PBRLightingDot(N, V);
	float NdotL = PBRLightingDot(N, L);
	float HdotV = PBRLightingDot(H, V);

	vec3 brdfF = PBRSchlickFresnel(HdotV, PBRFresnelColor(fragment.albedo.rgb, fragment.metallic));

	vec3 finalColor = PBRDirectContribution(brdfF, NdotH, NdotV, NdotL,
											vertex, fragment, light);
	finalColor += PBRIndirectContribution(brdfF, vertex, fragment);

	// Correction
	finalColor = finalColor / (finalColor + vec3(1.0));
	finalColor = pow(finalColor, vec3(1.0/2.2));
	
	return vec4(finalColor, fragment.albedo.a);
}
